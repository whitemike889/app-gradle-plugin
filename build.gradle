/*
 * Copyright (c) 2017 Google Inc. All Right Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2"
    }
  }
  dependencies {
    classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.6'
    classpath 'net.researchgate:gradle-release:2.6.0'
  }
}

apply plugin: 'java'
apply plugin: 'java-gradle-plugin'

repositories {
  mavenCentral()
}

apply from: 'scripts/releasing.gradle'
apply from: 'scripts/integTest.gradle'
apply from: 'scripts/google-java-format.gradle'
apply from: 'scripts/checkstyle.gradle'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

group = 'com.google.cloud.tools'

dependencies {
  compile localGroovy()
  compile gradleApi()
  compile 'com.google.cloud.tools:appengine-plugins-core:0.3.2'

  testCompile 'commons-io:commons-io:2.4'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-library:1.3'
  testCompile 'org.mockito:mockito-core:2.7.21'
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name,
        'Implementation-Version': version,
        'Built-By': System.getProperty('user.name'),
        'Built-Date': new Date(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion
  }
}

test {
  testLogging {
    showStandardStreams = true
    exceptionFormat = 'full'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.4.1'
}
